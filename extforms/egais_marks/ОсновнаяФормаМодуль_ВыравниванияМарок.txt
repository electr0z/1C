
Процедура КнопкаВыполнитьНажатие(Кнопка)
	ФормаИндикатора = ПолучитьОбщуюФорму("Индикация");
	ФормаИндикатора.Заголовок = "Очищаем регистр акцизные марки";
	ФормаИндикатора.СтрокаСостояния = "Сбор начальных данных...";
	ФормаИндикатора.Открыть();
	
	ТаблицаЗапросаАкцизныхМарок = ТаблицаЗапросаАкцизныхМарок();
	
	МаксимальноеЗначениеИндикатора = ТаблицаЗапросаАкцизныхМарок.Количество();
	
	ФормаИндикатора.ЭлементыФормы.Индикатор.МаксимальноеЗначение=МаксимальноеЗначениеИндикатора; 	
	ФормаИндикатора.ЭлементыФормы.Индикатор.Шаг=МаксимальноеЗначениеИндикатора/100;
	ФормаИндикатора.СтрокаСостояния = "Чистим данные...";
	Инд = 0;
	Для Каждого строкатаблицы из ТаблицаЗапросаАкцизныхМарок Цикл
		ОчиститьРегистрАкцизныхМарокПоОтбору(строкатаблицы);
		Инд = Инд + 1;				
		ФормаИндикатора.ЭлементыФормы.Индикатор.Значение=Инд;
	КонецЦикла;
	Если ФормаИндикатора.Открыта() Тогда
		ФормаИндикатора.Закрыть();
	КонецЕсли;
	Сообщить("Процедура выполнена!", СтатусСообщения.Внимание);
	
КонецПроцедуры

Функция ТаблицаЗапросаАкцизныхМарок()
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Статус", Перечисления.СтатусыМарокНаПредприятии.Продана); //убираем из отбора проданные марки
	Запрос.Текст = "ВЫБРАТЬ
	|	АкцизныеМаркиЕГАИС.АкцизнаяМарка,
	|	КОЛИЧЕСТВО(АкцизныеМаркиЕГАИС.АкцизнаяМарка) КАК Количество
	|ПОМЕСТИТЬ вт
	|ИЗ
	|	РегистрСведений.АкцизныеМаркиЕГАИС КАК АкцизныеМаркиЕГАИС
	|
	|СГРУППИРОВАТЬ ПО
	|	АкцизныеМаркиЕГАИС.АкцизнаяМарка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ВЫБОР
	|			КОГДА АкцизныеМаркиЕГАИС.Основание = НЕОПРЕДЕЛЕНО
	|				ТОГДА ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ИНАЧЕ АкцизныеМаркиЕГАИС.Основание.Дата
	|		КОНЕЦ) КАК ОснованиеДата,
	|	АкцизныеМаркиЕГАИС.АкцизнаяМарка
	|ПОМЕСТИТЬ вт2
	|ИЗ
	|	вт КАК вт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АкцизныеМаркиЕГАИС КАК АкцизныеМаркиЕГАИС
	|		ПО вт.АкцизнаяМарка = АкцизныеМаркиЕГАИС.АкцизнаяМарка
	|ГДЕ
	|	вт.Количество > 1
	|	И (ТИПЗНАЧЕНИЯ(АкцизныеМаркиЕГАИС.Основание) <> ТИП(Документ.СписаниеТоваровЕГАИС)
	|			ИЛИ АкцизныеМаркиЕГАИС.Основание = НЕОПРЕДЕЛЕНО)
	|	И АкцизныеМаркиЕГАИС.Статус <> &Статус
	|
	|СГРУППИРОВАТЬ ПО
	|	АкцизныеМаркиЕГАИС.АкцизнаяМарка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АкцизныеМаркиЕГАИС.АкцизнаяМарка,
	|	АкцизныеМаркиЕГАИС.ОрганизацияЕГАИС
	|ИЗ
	|	вт2 КАК вт2
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АкцизныеМаркиЕГАИС КАК АкцизныеМаркиЕГАИС
	|		ПО вт2.АкцизнаяМарка = АкцизныеМаркиЕГАИС.АкцизнаяМарка
	|			И (вт2.ОснованиеДата = ВЫБОР
	|				КОГДА АкцизныеМаркиЕГАИС.Основание = НЕОПРЕДЕЛЕНО
	|					ТОГДА ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ИНАЧЕ АкцизныеМаркиЕГАИС.Основание.Дата
	|			КОНЕЦ)";
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ОчиститьРегистрАкцизныхМарокПоОтбору(строкатаблицы)
	РС = РегистрыСведений.АкцизныеМаркиЕГАИС.СоздатьНаборЗаписей();
	РС.Отбор.ОрганизацияЕГАИС.Установить(строкатаблицы.ОрганизацияЕГАИС);
	РС.Отбор.АкцизнаяМарка.Установить(строкатаблицы.АкцизнаяМарка);
	Попытка
		РС.Записать();
	Исключение
		Сообщить("Не удалось удалить марку: " + строкатаблицы.АкцизнаяМарка + " По организации: " 
		+ строкатаблицы.ОрганизацияЕГАИС);
	КонецПопытки;
КонецПроцедуры
